************************************************************************
*								       *
*		044 [RYUUKO NO KEN]				       *
*			DEMO PROGRAM				       *
*		by S.OKADA from 92/05/26 Tue 10:25		       *
*								       *
************************************************************************



		XDEF	DEMO_GAME,INIT_DEMO_GAME
		XDEF	TITLE,INIT_TITLE,TITLE_ONLY,INIT_TITLE_ONLY
		XDEF	PM_EASY,LM_EASY
		XDEF	DEMO_OBJ_MAP
		XDEF	MASTER_CHECK
		XDEF	INIT_TI_DEMO
		XDEF	INIT_TI_DEMO_ONLY


		XREF	?A5
;by PHASE
		XREF	ACTIVE_TRIGER
;by EASY
		XREF	INIT_MONO_SET
		XREF	MONO_SET
;by MESSAGE
		XREF	MESS_SET,MESS_SET_INNER,FLAME_WRITE,FLAME_CLR
;by LANG
		XREF	LANG_SET,LANG_SET_INNER
;by BONUS
		XREF	INIT_BONUS1
;by GAME_SUB
		XREF	LWORK_DEAD
;by STORY
		XREF	INIT_STORY_GAME,STORY_GAME
;by RANK
		XREF	RANKING_DISP

		SECT	GAME,,C

		INCLUDE	SYS.INC
		INCLUDE	NEO_GEO.INC
		INCLUDE	LABEL.INC
		INCLUDE	ACT_No.INC
		INCLUDE	MACRO.INC
		INCLUDE	EASY_MAC.INC
		INCLUDE	SMACRO.INC
		INCLUDE	SCODE.INC



INIT_DEMO_GAME:
		MOVE.B	#1,PAUSE_DI(A5)
		CLR.B	RANDOM_SEED(A5)
		MOVE.W	STORY_STAGE(A5),AREA_No(A5)
		JMP	INIT_STORY_GAME(PC)

DEMO_GAME:
		MOVE.B	#1,PAUSE_DI(A5)
		MOVE.W	STORY_STAGE(A5),AREA_No(A5)
		JSR	MESS_START(PC)
		LEA.L	MLWORK2(A5),A6
		EASY_START
DEMO_GAME_1:
		JSR	STORY_GAME(PC)

		MOVE.B	#1,PAUSE_DI(A5)
		LEA.L	MLWORK2(A5),A6
		TST.W	HERO1+DEMO_LEV_COUNT(A5)
		BEQ	RANKING_DISP

		RTS


DEMO_GAME_SET:
		LEA.L		MLWORK2(A5),A6
		STEP_SAVE	1,DEMO_GAME_1
		CLR.W		SUB_STEP(A5)
		MOVE.B		PHASE+1(A5),D0
		AND.B		#11110000B,D0
		OR.B		#10000000B,D0
		MOVE.B		D0,PHASE+1(A5)
		MOVE.W		#2,LEVEL_TEMP(A5)
		LEA.L		DEMO1_DATA(PC),A0
		BTST.B		#0,DEMO_NUMBER(A5)
		BNE.S		DGS_1

		LEA.L		DEMO2_DATA(PC),A0
DGS_1:
		MOVE.L		(A0)+,HERO1+DEMO_LEVER(A5)
		MOVE.L		(A0)+,HERO2+DEMO_LEVER(A5)
		MOVE.W		(A0)+,HERO1+HERO_TYPE(A5)
		MOVE.W		(A0)+,HERO2+HERO_TYPE(A5)
		MOVE.W		(A0)+,STORY_STAGE(A5)
		MOVE.W		#400H,HERO1+DEMO_LEV_COUNT(A5)
		MOVE.W		#400H,HERO2+DEMO_LEV_COUNT(A5)
		MOVEQ.L		#DEMO_IN,D0
		AND.B		DEBUG_DIP1(A5),D0
		BEQ.S		DGS_2

		ADD.L		#200000H,HERO1+DEMO_LEVER(A5)
		ADD.L		#200000H,HERO2+DEMO_LEVER(A5)
DGS_2:
		RTS
DEMO1_DATA:
		DC.L		11000H,11400H
		DC.W		0,2
		DC.W		1
DEMO2_DATA:
		DC.L		11800H,11C00H
		DC.W		4,1
		DC.W		4




;
;
;	    *** TITLE ***
;
;


DEMO_OBJ_MAP:
		DC.W		031H,14CH,0	obj 1	380-48
		DC.W		001H,030H,-1	obj 2
		DC.W		000H,000H,0	obj 3
		DC.W		000H,000H,0	obj 4
		DC.W		-1		div


INIT_TITLE:
		LEA.L		MLWORK1(A5),A6
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+FADE_COLOR
		PALETTE		080H,20H,PG_SET+FADE_COLOR
		PALETTE		0D0H,25H,PG_SET+FADE_COLOR
		PALETTE		0E0H,26H,PG_SET+FADE_COLOR
		PALETTE_END

		MESS_ON
		MOVE.L		#M_FIX_CLEAR,(A0)+
		MESS_OFF

		CLR.W		INSERT_POS(A5)
		OBJ_MAP		DEMO_OBJ_MAP
		CLR.L		WINDOW_X(A5)
		CLR.L		WINDOW_Y(A5)
		STEP_SAVE	0,PHOTO_0
		AND.B		#01111111B,PHASE+1(A5)
		RTS


PHOTO_0:
		PAL_WAIT	0EFH

		PCHILD		WORK_N0,P_EASY,C_PHOTO,0,0,0,0
		PCHILD		WORK_N1,P_EASY,C_PHOTO,0,0,1,0
		MOVE.B		#0,ACT_COUNT(A0)
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+FADE_IN+CC_SPEED0
		PALETTE		080H,20H,PG_SET+FADE_IN+CC_SPEED0
		PALETTE		0D0H,25H,PG_SET+FADE_IN+CC_SPEED0
		PALETTE		0E0H,26H,PG_SET+FADE_IN+CC_SPEED0
		PALETTE_END
		END_NEXT	1,PHOTO_1
PHOTO_1:
		PAL_WAIT	0EFH

		SEC_TIMER_SET	0,WORD_TIMER
		END_NEXT	11,PHOTO_11
PHOTO_11:
		SEC_TIMER	WORD_TIMER
		NEXT_CHECK	BCS,12,PHOTO_12

		SOUND		#S_GLASS_DM
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+RGB_COLOR
		PALETTE		080H,20H,PG_SET+RGB_COLOR
		PALETTE		0D0H,25H,PG_SET+RGB_COLOR
		PALETTE		0E0H,26H,PG_SET+RGB_COLOR
		PALETTE_END
		RTS
PHOTO_12:
		PAL_WAIT	0EFH

		SUB_WORK	WORK_N1,A0,0
		MOVE.B		#1,ACT_DEC(A0)
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+BASE_COLOR
		PALETTE		080H,20H,PG_SET+BASE_COLOR
		PALETTE		0D0H,25H,PG_SET+BASE_COLOR
		PALETTE		0E0H,26H,PG_SET+BASE_COLOR
		PALETTE_END
		END_NEXT	13,PHOTO_13
PHOTO_13:
		PAL_WAIT	0EFH

		SEC_TIMER_SET	0,WORD_TIMER
		END_NEXT	2,PHOTO_2
PHOTO_2:
		SEC_TIMER	WORD_TIMER
		NEXT_CHECK	BCS,3,PHOTO_3

		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+FADE_OUT+CC_SPEED0
		PALETTE		080H,20H,PG_SET+FADE_OUT+CC_SPEED0
		PALETTE		0D0H,25H,PG_SET+FADE_OUT+CC_SPEED1
		PALETTE		0E0H,26H,PG_SET+FADE_OUT+CC_SPEED0
		PALETTE_END
		RTS
PHOTO_3:
		PAL_WAIT	0EFH

		SUB_WORK	WORK_N0,A0,0
		CLR.W		TYPE(A0)
		SUB_WORK	WORK_N1,A0,0
		CLR.W		TYPE(A0)

INIT_TITLE_0:
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+FADE_COLOR
		PALETTE		010H,04H,PG_SET+FADE_COLOR
		PALETTE		020H,05H,PG_SET+FADE_COLOR
		PALETTE		030H,06H,PG_SET+FADE_COLOR
		PALETTE		040H,0DH,PG_SET+FADE_COLOR
		PALETTE		050H,0EH,PG_SET+FADE_COLOR
		PALETTE		060H,0FH,PG_SET+FADE_COLOR
		PALETTE		070H,10H,PG_SET+FADE_COLOR
		PALETTE		080H,20H,PG_SET+FADE_COLOR
		PALETTE		090H,21H,PG_SET+FADE_COLOR
		PALETTE		0A0H,22H,PG_SET+FADE_COLOR
		PALETTE		0B0H,23H,PG_SET+FADE_COLOR
		PALETTE		0C0H,24H,PG_SET+FADE_COLOR
		PALETTE		0D0H,25H,PG_SET+FADE_COLOR
		PALETTE_END

		END_NEXT	0,GAME_DEMO_0

TITLE:
		JSR		MESS_START(PC)
		LEA.L		MLWORK1(A5),A6
		EASY_START

GAME_DEMO_0:
		PAL_WAIT	0DFH

		MOVE.W		#S_TITLE,D0
		MOVE.W		V_POSITION,D1
		BTST.L		#3,D1
		BEQ		GDD_0_1

		MOVE.W		#S_TITLE2,D0
GDD_0_1:
		JSR.S		SET_BGM
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		010H,04H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		020H,05H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		030H,06H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		040H,0DH,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		050H,0EH,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		060H,0FH,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		070H,10H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		080H,20H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		090H,21H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		0A0H,22H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		0B0H,23H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		0C0H,24H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE		0D0H,25H,PG_SET+FADE_IN+CC_SPEED3
		PALETTE_END

		MOVE.W		#0A00H,D0
		MOVE.B		LSPC_MODE_STORE+1(A5),D0
		BCLR.L		#3,D0			auto action on
		MOVE.W		D0,LSPC_MODE_STORE(A5)
		MOVE.W		D0,LSPC_MODE

		PCHILD		WORK_N1,P_EASY,C_GD_BACK2,319,255-32,1,0
		BSET.B		#ACTAT_KEEP,ACT_ATTR(A0)
		BSET.B		#ACTAT_CUT,ACT_ATTR(A0)
		MOVE.B		#7,BIG_X(A0)
		PCHILD		WORK_N0,P_EASY,C_GD_BACK1,0,255-32,3,0
		BSET.B		#ACTAT_CUT,ACT_ATTR(A0)
		PCHILD		WORK_N2,P_EASY,C_GD_H1,192,40H-32,5,0
		BSET.B		#ACTAT_FLIP,ACT_ATTR(A0)
		MOVE.W		X_POSITION(A0),VX(A0)
		LEA.L		(A0),A2
		PCHILD		WORK_N3,P_EASY,C_GD_E1,128,40H-32,4,0
		MOVE.B		WORK_No(A0),WORK_N0(A2)
		MOVE.W		X_POSITION(A0),VX(A0)
		END_NEXT	1,GAME_DEMO_1
GAME_DEMO_1:
		PAL_WAIT	0D0H

		END_NEXT	2,GAME_DEMO_2
GAME_DEMO_2:
		SUB_WORK	WORK_N2,A1,0
		TST.B		ACT_FLAG(A1)
		BMI.S		GAME_DEMO_3

		RTS
GAME_DEMO_3:
		MOVEP.W		WORK_N0(A1),D0
		CLR.B		D0
		LEA.L		0(A5,D0.W),A0
		TST.B		ACT_FLAG(A0)
		NEXT_CHECK	BMI,4,GAME_DEMO_4

		MOVE.W		LSPC_MODE_STORE(A5),D0
		BSET.L		#3,D0			auto action on
		MOVE.W		D0,LSPC_MODE_STORE(A5)
		MOVE.W		D0,LSPC_MODE
		MOVE.W		#0,FLAME_COUNT(A5)
		MOVE.W		#0,FLAME_TYPE(A5)
		MOVE.W		#7000H+20H*3+24,FLAME_POS(A5)
		MOVE.W		#4,FLAME_END(A5)
		RTS
GAME_DEMO_4:
		JSR		FLAME_WRITE(PC)
		NEXT_CHECK	BEQ,5,GAME_DEMO_5

		MOVE.W		#0,D0
		JSR		LANG_SET(PC)

		MOVE.W		#10H,BYTE_FLAG+80H(A5)
		MOVE.W		#30H,BYTE_FLAG+82H(A5)
		MOVE.W		#16,BYTE_FLAG+84H(A5)
		MOVE.W		#16H,REG_D0(A5)
		MOVE.W		#16H,REG_D0+2(A5)
		MOVE.W		#16H,REG_D0+4(A5)
		MOVE.W		#08H,REG_D0+6(A5)
		MOVE.W		#08H,REG_D0+8(A5)
		MOVE.W		#08H,REG_D0+10(A5)
		JSR		INIT_MONO_SET(PC)

		SEC_TIMER_SET	4,WORD_TIMER
		RTS
GAME_DEMO_5:
		MOVE.W		#10H,BYTE_FLAG+80H(A5)
		MOVE.W		#30H,BYTE_FLAG+82H(A5)
		JSR		MONO_SET(PC)

		MOVE.W		#0A6H,BYTE_FLAG+80H(A5)
		MOVE.W		#06H,BYTE_FLAG+82H(A5)
		MOVE.W		BYTE_FLAG+84H(A5),D0
		SUB.W		D0,BYTE_FLAG+86H(A5)
		JSR		MONO_SET(PC)

		SEC_TIMER	WORD_TIMER
		NEXT_CHECK	BCS,6,GAME_DEMO_6

		MOVE.W		#0,FLAME_END(A5)
		RTS
GAME_DEMO_6:
		JSR		FLAME_CLR(PC)
		NEXT_CHECK	BEQ,7,GAME_DEMO_7

		SUB_WORK	WORK_N1,A2,0
		BCLR.B		#ACTAT_KEEP,ACT_ATTR(A2)
		PCHILD		WORK_N3,P_EASY,C_GD_E12,192,40H-32,6,0
		BSET.B		#ACTAT_FLIP,ACT_ATTR(A0)
		MOVE.W		X_POSITION(A0),VX(A0)
		LEA.L		(A0),A3
		PCHILD		WORK_N3,P_EASY,C_GD_H2,128,40H-32,7,0
		MOVE.W		X_POSITION(A0),VX(A0)
		LEA.L		(A0),A1
		LEA.L		(A3),A0
		MOVE.B		WORK_No(A0),WORK_N0(A1)
		BSR		RED_POS
		RTS
GAME_DEMO_7:
		SUB_WORK	WORK_N1,A2,0
		SUB.W		#10,X_POSITION(A2)
		ADDQ.B		#8,BIG_X(A2)
		BCC.S		GAME_DEMO_8

		MOVE.B		#0FFH,BIG_X(A2)
GAME_DEMO_8:
		SUB_WORK	WORK_N3,A1,0
		BSR		RED_POS

		SUB_WORK	WORK_N0,A2,0
		SUBQ.B		#8,BIG_X(A2)
		SUB_WORK	WORK_N2,A1,0
		BSR		RED_POS

		CMP.B		#7,BIG_X(A2)
		NEXT_CHECK	BEQ,9,GAME_DEMO_9

		MOVE.W		#0700H,D0
		MOVE.B		LSPC_MODE_STORE+1(A5),D0
		BCLR.L		#3,D0			auto action on
		MOVE.W		D0,LSPC_MODE_STORE(A5)
		MOVE.W		D0,LSPC_MODE
		RTS
GAME_DEMO_9:
		SUB_WORK	WORK_N1,A2,0
		MOVE.W		#0,X_POSITION(A2)
		MOVE.B		#0FFH,BIG_X(A2)
		SUB_WORK	WORK_N3,A1,0
		BSR		RED_POS

		SUB_WORK	WORK_N0,A2,0
		MOVE.W		#-32,X_POSITION(A2)
		SUB_WORK	WORK_N2,A1,0
		BSR		RED_POS
		END_NEXT	10,GAME_DEMO_10
GAME_DEMO_10:
		SUB_WORK	WORK_N3,A1,0
		TST.B		ACT_FLAG(A1)
		BMI.S		GAME_DEMO_11

		RTS
GAME_DEMO_11:
		MOVEP.W		WORK_N0(A1),D0
		CLR.B		D0
		LEA.L		0(A5,D0.W),A0
		TST.B		ACT_FLAG(A0)
		NEXT_CHECK	BMI,12,GAME_DEMO_12

		MOVE.W		LSPC_MODE_STORE(A5),D0
		BSET.L		#3,D0			auto action on
		MOVE.W		D0,LSPC_MODE_STORE(A5)
		MOVE.W		D0,LSPC_MODE
		MOVE.W		#0,FLAME_COUNT(A5)
		MOVE.W		#0,FLAME_TYPE(A5)
		MOVE.W		#7000H+20H*3+24,FLAME_POS(A5)
		MOVE.W		#4,FLAME_END(A5)
		RTS
GAME_DEMO_12:
		JSR		FLAME_WRITE(PC)
		NEXT_CHECK	BEQ,13,GAME_DEMO_13

		MOVE.W		#1*3,D0
		JSR		LANG_SET(PC)

		MOVE.W		#40H,BYTE_FLAG+80H(A5)
		MOVE.W		#40H,BYTE_FLAG+82H(A5)
		MOVE.W		#16,BYTE_FLAG+84H(A5)
		MOVE.W		#16H,REG_D0(A5)
		MOVE.W		#16H,REG_D0+2(A5)
		MOVE.W		#16H,REG_D0+4(A5)
		MOVE.W		#08H,REG_D0+6(A5)
		MOVE.W		#08H,REG_D0+8(A5)
		MOVE.W		#08H,REG_D0+10(A5)
		JSR		INIT_MONO_SET(PC)

		SEC_TIMER_SET	5,WORD_TIMER
		RTS
GAME_DEMO_13:
		MOVE.W		#40H,BYTE_FLAG+80H(A5)
		MOVE.W		#40H,BYTE_FLAG+82H(A5)
		JSR		MONO_SET(PC)

		MOVE.W		#0B8H,BYTE_FLAG+80H(A5)
		MOVE.W		#06H,BYTE_FLAG+82H(A5)
		MOVE.W		BYTE_FLAG+84H(A5),D0
		SUB.W		D0,BYTE_FLAG+86H(A5)
		JSR		MONO_SET(PC)

		SEC_TIMER	WORD_TIMER
		NEXT_CHECK	BCS,14,GAME_DEMO_14

		MOVE.W		#0,FLAME_END(A5)
		RTS
GAME_DEMO_14:
		JSR		FLAME_CLR(PC)
		NEXT_CHECK	BEQ,17,GAME_DEMO_17

		SUB_WORK	WORK_N0,A0,0
		MOVE.W		#0,X_POSITION(A0)
		MOVE.B		#7H,BIG_X(A0)
		RTS

		NEXT_CHECK	BCS,15,GAME_DEMO_15

		SUB_WORK	WORK_N0,A0,0
		MOVE.W		#-160,X_POSITION(A0)
		MOVE.B		#7FH,BIG_X(A0)
		RTS
GAME_DEMO_15:
		SUB_WORK	WORK_N0,A2,0
		ADD.B		#8,BIG_X(A2)
		SUB_WORK	WORK_N2,A1,0
		BSR		RED_POS

		CMP.B		#0A7H,BIG_X(A2)
		BNE.S		GAME_DEMO_16

		STEP_SAVE	17,GAME_DEMO_17
GAME_DEMO_16:
		RTS
GAME_DEMO_17:
		SUB_WORK	WORK_N1,A2,0
		ADD.W		#10,X_POSITION(A2)
		SUB.B		#8,BIG_X(A2)		@@@@@
		SUB_WORK	WORK_N3,A1,0
		BSR		RED_POS

		SUB_WORK	WORK_N0,A2,0
		ADD.B		#8,BIG_X(A2)
		SUB_WORK	WORK_N2,A1,0
		BSR		RED_POS

		CMP.B		#07FH,BIG_X(A2)		@@@@@
		NEXT_CHECK	BEQ,18,GAME_DEMO_18

		MOVE.W		#0,FLAME_COUNT(A5)
		MOVE.W		#0,FLAME_TYPE(A5)
		MOVE.W		#7000H+20H*3+24,FLAME_POS(A5)
		MOVE.W		#4,FLAME_END(A5)
		RTS
GAME_DEMO_18:
		JSR		FLAME_WRITE(PC)
		NEXT_CHECK	BEQ,19,GAME_DEMO_19

		MOVE.W		#2*3,D0
		JSR		LANG_SET(PC)

		SEC_TIMER_SET	1,WORD_TIMER
;		SEC_TIMER_SET	3,WORD_TIMER
		RTS
GAME_DEMO_19:
		SEC_TIMER	WORD_TIMER
		NEXT_CHECK	BCS,21,GAME_DEMO_21

		LEA.L		COLOR_BUFFER+10H*20H(A5),A0
		MOVE.W		#FADE_OUT+CC_SPEED3,D0
		MOVE.W		D0,COLOR_BUFFER+5*20H(A5)
		MOVE.W		D0,COLOR_BUFFER+7*20H(A5)
		MOVE.W		#0F0H-1,D7
GAME_DEMO_20:
		MOVE.W		D0,(A0)
		LEA.L		20H(A0),A0
		DBRA		D7,GAME_DEMO_20

		MOVE.B		#1,COLOR_STATE(A5)
		RTS
GAME_DEMO_21:
		PAL_WAIT	0FFH

		MESS_ON
		MOVE.L		#M_FIX_CLEAR,(A0)+
		MESS_OFF

		SUB_WORK	WORK_N0,A0,0
		CLR.W		TYPE(A0)
		SUB_WORK	WORK_N1,A0,0
		CLR.W		TYPE(A0)
		SUB_WORK	WORK_N2,A0,0
		CLR.W		TYPE(A0)
		MOVEP.W		WORK_N0(A0),D0
		CLR.B		D0
		CLR.W		TYPE(A5,D0.W)
		SUB_WORK	WORK_N3,A0,0
		CLR.W		TYPE(A0)
		MOVEP.W		WORK_N0(A0),D0
		CLR.B		D0
		CLR.W		TYPE(A5,D0.W)

		JSR		INIT_BONUS1(PC)
		RTS




RED_POS:
		MOVEP.W		WORK_N0(A1),D0
		CLR.B		D0
		LEA.L		0(A5,D0.W),A0
		MOVEQ.L		#0,D0
		MOVE.B		BIG_X(A2),D0
		MOVE.B		D0,BIG_X(A1)
		MOVE.B		D0,BIG_X(A0)
		ADDQ.W		#1,D0
		MOVE.W		VX(A1),D1
		MULU		D0,D1
		LSR.L		#8,D1
		ADD.W		X_POSITION(A2),D1
		MOVE.W		D1,X_POSITION(A1)
		MOVE.W		VX(A0),D1
		MULU		D0,D1
		LSR.L		#8,D1
		ADD.W		X_POSITION(A2),D1
		MOVE.W		D1,X_POSITION(A0)
		RTS



INIT_TI_DEMO:
		MOVE.L		A6,-(SP)
		JSR.S		PWORK_INIT
		MOVE.L		(SP)+,A6

		TST.B		COUNTRY_CODE(A5)
		BNE		INIT_ELOGO

		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+RGB_COLOR
		PALETTE		0F0H,27H,PG_SET+RGB_COLOR
		PALETTE_END

		MOVE.W		#1,INSERT_POS(A5)
		MESS_ON
		MOVE.L		#M_FIX_CLEAR,(A0)+
		MOVEQ.L		#0,D0
		MOVE.B		COUNTRY_CODE(A5),D0
		ADDQ.W		#3,D0
		TST.B		MVS_FLAG(A5)
		BNE.S		INIT_TI_DEMO_1

		ADDQ.W		#3,D0
INIT_TI_DEMO_1:
		JSR.S		MESS_SET_INNER
		MESS_OFF

		PCHILD		WORK_N0,P_EASY,C_TITLE_BACK,160,50H+56,1,0
		MOVE.B		#7,BIG_X(A0)
		MOVE.B		#7,BIG_Y(A0)
		STEP_SAVE	0,TI_DEMO_0
		RTS


TI_DEMO_0:
		PAL_WAIT	0FFH

;		MOVE.W		#0,COLOR_BUFFER+1FFEH(A5)
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+RGB_IN+CC_SPEED1
		PALETTE		0F1H,00H,RGB_IN+CC_SPEED1
		PALETTE		0FFH,00H,RGB_IN+CC_SPEED1
		PALETTE_END

		END_NEXT	1,TI_DEMO_1
TI_DEMO_1:
		SUB_WORK	WORK_N0,A0,0
		ADDQ.B		#8,BIG_X(A0)
		ADDQ.B		#8,BIG_Y(A0)
		CMP.B		#0FFH,BIG_X(A0)
		NEXT_CHECK	BEQ,2,TI_DEMO_2

		MOVE.B		#12,BYTE_TIMER(A6)
		RTS
TI_DEMO_2:
		SUBQ.B		#1,BYTE_TIMER(A6)
		NEXT_CHECK	BEQ,3,TI_DEMO_3

		COLOR_COMMAND
		PALETTE		0F0H,00H,RGB_IN+CC_SPEED1
		PALETTE_END

		PCHILD		WORK_N1,P_EASY,C_TITLE_LOGO,160,68H,2,0

		MOVE.W		#0EAH,D0
		ADD.B		COUNTRY_CODE(A5),D0
		JSR.S		MESS_SET
		RTS
TI_DEMO_3:
		PAL_WAIT	0F0H

		MOVE.W		#0F1H,BYTE_FLAG+80H(A5)
		MOVE.W		#1H,BYTE_FLAG+82H(A5)
		MOVE.W		#4,BYTE_FLAG+84H(A5)
		MOVE.W		#1CH,REG_D0(A5)
		MOVE.W		#1AH,REG_D0+2(A5)
		MOVE.W		#12H,REG_D0+4(A5)
		MOVE.W		#00H,REG_D0+6(A5)
		MOVE.W		#00H,REG_D0+8(A5)
		MOVE.W		#00H,REG_D0+10(A5)
		JSR		INIT_MONO_SET(PC)

		SEC_TIMER_SET	6,WORD_TIMER
		END_NEXT	4,TI_DEMO_4
TI_DEMO_4:
		JSR		MONO_SET(PC)

		SEC_TIMER	WORD_TIMER
		BCC.S		TI_DEMO_7

		JMP		DEMO_GAME_SET(PC)
TI_DEMO_7:
		RTS




INIT_TI_DEMO_ONLY:
		MOVE.L		A6,-(SP)
		JSR.S		PWORK_INIT
		JSR		LWORK_DEAD(PC)
		MOVE.L		(SP)+,A6

		TST.B		COUNTRY_CODE(A5)
		BNE		INIT_ELOGO_ONLY

		MOVE.W		#1,INSERT_POS(A5)

		LCHILD		WORK_N0,L_EASY,TI_DEMO_ONLY_0

		OBJ_MAP		DEMO_OBJ_MAP
		CLR.L		WINDOW_X(A5)
		CLR.L		WINDOW_Y(A5)
		RTS


TI_DEMO_ONLY_0:
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+BASE_COLOR
		PALETTE		0F0H,27H,PG_SET+BASE_COLOR
		PALETTE_END

		END_NEXT	1,TI_DEMO_ONLY_1
TI_DEMO_ONLY_1:
		COLOR_COMMAND
		PALETTE		0F0H,00H,RGB_IN+CC_SPEED1
		PALETTE_END

		PCHILD		WORK_N0,P_EASY,C_TITLE_BACK,160,50H+56,1,0
		PCHILD		WORK_N1,P_EASY,C_TITLE_LOGO,160,68H,2,0
		END_NEXT	2,TI_DEMO_ONLY_2
		RTS
TI_DEMO_ONLY_2:
		PAL_WAIT	0F0H

		MOVE.W		#0F1H,BYTE_FLAG+80H(A5)
		MOVE.W		#1H,BYTE_FLAG+82H(A5)
		MOVE.W		#4,BYTE_FLAG+84H(A5)
		MOVE.W		#1CH,REG_D0(A5)
		MOVE.W		#1AH,REG_D0+2(A5)
		MOVE.W		#12H,REG_D0+4(A5)
		MOVE.W		#00H,REG_D0+6(A5)
		MOVE.W		#00H,REG_D0+8(A5)
		MOVE.W		#00H,REG_D0+10(A5)
		JSR		INIT_MONO_SET(PC)

		END_NEXT	3,TI_DEMO_ONLY_3
TI_DEMO_ONLY_3:
		JSR		MONO_SET(PC)

		CMP.W		#100H,BYTE_FLAG+86H(A5)
		BLS.S		TDO_2_1

		CLR.W		TYPE(A6)
TDO_2_1:
		RTS


INIT_TITLE_ONLY:
		LEA.L		MLWORK1(A5),A6
		JSR		INIT_TI_DEMO_ONLY(PC)

		MESS_ON
		MOVE.L		#M_FIX_CLEAR,(A0)+
		MOVEQ.L		#0,D0
		MOVE.B		COUNTRY_CODE(A5),D0
		ADDQ.W		#3,D0
		TST.B		MVS_FLAG(A5)
		BNE.S		TDO_1_1

		ADDQ.W		#3,D0
TDO_1_1:
		JSR.S		MESS_SET_INNER

		MOVE.W		#0EAH,D0
		ADD.B		COUNTRY_CODE(A5),D0
		JSR.S		MESS_SET_INNER
		MESS_OFF

		MOVE.W		#S_MVSTITLE,D0
		JSR.S		SET_BGM

		AND.B		#01111111B,PHASE+1(A5)
		RTS

TITLE_ONLY:
		JSR		MESS_START(PC)
		RTS

INIT_ELOGO_ONLY:
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+BASE_COLOR
		PALETTE		0F0H,29H,PG_SET+RGB_IN+CC_SPEED2
		PALETTE_END

		MOVE.W		#1,INSERT_POS(A5)
		LCHILD		WORK_N3,L_EASY,ELOGO_ONLY_0
		OBJ_MAP		DEMO_OBJ_MAP
		CLR.L		WINDOW_X(A5)
		CLR.L		WINDOW_Y(A5)
		RTS

ELOGO_ONLY_0:
		PCHILD		WORK_N0,P_EASY,C_LOGO,88,220,0,0
		MOVE.B		#0,ACT_COUNT(A0)
		PCHILD		WORK_N0,P_EASY,C_LOGO,216,176,1,0
		MOVE.B		#2,ACT_COUNT(A0)
		PCHILD		WORK_N0,P_EASY,C_LOGO,160,176,2,0
		MOVE.B		#4,ACT_COUNT(A0)
		PCHILD		WORK_N0,P_EASY,C_LOGO,88,220,4,0
		PCHILD		WORK_N1,P_EASY,C_LOGO,216,176,3,0
		MOVE.B		#1,ACT_COUNT(A0)
		PCHILD		WORK_N2,P_EASY,C_LOGO,160,176,5,0
		MOVE.B		#3,ACT_COUNT(A0)

		MOVE.B		#3,BYTE_TIMER(A6)
		END_NEXT	1,ELOGO_ONLY_1
ELOGO_ONLY_1:
		SUB_WORK	WORK_N0,A0,0
		ADDQ.W		#1,Y_POSITION(A0)
		SUB_WORK	WORK_N1,A0,0
		ADDQ.W		#1,Y_POSITION(A0)
		SUB_WORK	WORK_N2,A0,0
		ADDQ.W		#1,Y_POSITION(A0)
		SUBQ.B		#1,BYTE_TIMER(A6)
		NEXT_CHECK	BEQ,2,ELOGO_ONLY_2

		MOVE.B		#4,BYTE_TIMER(A6)
		RTS
ELOGO_ONLY_2:
		SUB_WORK	WORK_N0,A0,0
		ADDQ.W		#1,Y_POSITION(A0)
		SUBQ.W		#1,X_POSITION(A0)
		SUB_WORK	WORK_N1,A0,0
		ADDQ.W		#1,Y_POSITION(A0)
		SUBQ.W		#1,X_POSITION(A0)
		SUB_WORK	WORK_N2,A0,0
		ADDQ.W		#1,Y_POSITION(A0)
		SUBQ.W		#1,X_POSITION(A0)
		SUBQ.B		#1,BYTE_TIMER(A6)
		BNE.S		ELOGO_ONLY_1_1

		CLR.W		TYPE(A6)
ELOGO_ONLY_1_1:
		RTS


INIT_ELOGO:
		COLOR_COMMAND
		PALETTE		000H,00H,PG_SET+BASE_COLOR
		PALETTE		0F0H,29H,PG_SET+RGB_IN+CC_SPEED2
		PALETTE_END

		MESS_ON
		MOVE.L		#M_FIX_CLEAR,(A0)+
		MESS_OFF

		MOVE.W		#1,INSERT_POS(A5)
		CLR.L		WINDOW_X(A5)
		CLR.L		WINDOW_Y(A5)
		STEP_SAVE	0,ELOGO_0
		AND.B		#01111111B,PHASE+1(A5)
		RTS

ELOGO_0:
		PCHILD		WORK_N0,P_EASY,C_LOGO,-96,220,4,0
		PCHILD		WORK_N1,P_EASY,C_LOGO,216,176,3,0
		MOVE.B		#1,ACT_COUNT(A0)
		MOVE.B		#0FH,BIG_X(A0)
		MOVE.B		#0FH,BIG_Y(A0)
		BSET.B		#ACTAT_ERACE,ACT_ATTR(A0)
		END_NEXT	1,ELOGO_1
ELOGO_1:
		SUB_WORK	WORK_N0,A1,0
		ADD.W		#12,X_POSITION(A1)
		SUB_WORK	WORK_N1,A0,0
		ADD.B		#10H,BIG_Y(A0)
		ADD.B		#10H,BIG_X(A0)
		CMP.B		#0FFH,BIG_X(A0)
		NEXT_CHECK	BEQ,3,ELOGO_3

		CLR.B		BYTE_TIMER(A1)
		PCHILD		WORK_N2,P_EASY,C_LOGO,160,16,5,0
		MOVE.B		#3,ACT_COUNT(A0)
		BSET.B		#ACTAT_ERACE,ACT_ATTR(A0)
		MOVE.B		#1FH,BIG_Y(A0)
		RTS
ELOGO_3:
		SUB_WORK	WORK_N0,A0,0
		ADDQ.W		#2,X_POSITION(A0)
		ADDQ.B		#1,BYTE_TIMER(A0)
		SUB_WORK	WORK_N2,A0,0
		ADD.W		#5,Y_POSITION(A0)
		ADD.B		#20H,BIG_Y(A0)
		CMP.B		#0FFH,BIG_Y(A0)
		NEXT_CHECK	BEQ,4,ELOGO_4

		MOVE.W		SPRITE_POS(A0),D4
		CLR.W		WORD_TIMER(A6)
		BSR		ELOGO_POS1

		MOVE.W		#159,INT2_COUNT_BASE(A5)
		MOVE.L		#(39+64)*384-64,LSPC_TIMER1(A5)
		MOVE.W		V_POSITION,D0
		BTST.L		#3,D0
		BEQ.S		ELOGO_3_1		NTSC mode

		MOVE.L		#(39+64-16)*384-64,LSPC_TIMER1(A5)
ELOGO_3_1:
		MOVE.L		#384,LSPC_TIMER2(A5)
		MOVE.W		LSPC_MODE_STORE(A5),D0
		OR.B		#11010000B,D0
		MOVE.W		D0,LSPC_MODE_STORE(A5)
		MOVE.W		D0,LSPC_MODE
		MOVE.W		LSPC_TIMER1(A5),TIMER_HI
		MOVE.W		LSPC_TIMER1+2(A5),TIMER_LOW
		RTS
ELOGO_4:
		SUB_WORK	WORK_N0,A0,0
		TST.B		BYTE_TIMER(A0)
		BEQ.S		ELOGO_4_1

		SUBQ.W		#2,X_POSITION(A0)
		SUBQ.B		#1,BYTE_TIMER(A0)
ELOGO_4_1:
		SUB_WORK	WORK_N2,A0,0
		MOVE.W		SPRITE_POS(A0),D4
		BSR		ELOGO_POS1

		ADDQ.W		#1,WORD_TIMER(A6)
		CMP.W		#20,WORD_TIMER(A6)
		NEXT_CHECK	BEQ,5,ELOGO_5

		RTS
ELOGO_5:
		SUB_WORK	WORK_N2,A0,0
		MOVE.W		SPRITE_POS(A0),D4
		SUBQ.W		#1,WORD_TIMER(A6)
		BSR		ELOGO_POS2

		CMP.W		#0,WORD_TIMER(A6)
		NEXT_CHECK	BEQ,6,ELOGO_6

		SUB_WORK	WORK_N2,A0,0
		MOVE.W		#176,Y_POSITION(A0)
		RTS
ELOGO_6:
		MOVE.W		LSPC_MODE_STORE(A5),D0
		CLR.B		D0
		MOVE.W		D0,LSPC_MODE
		MOVE.W		D0,LSPC_MODE_STORE(A5)
		END_NEXT	7,ELOGO_7
ELOGO_7:
		SUB_WORK	WORK_N2,A2,0
		SUB.B		#20H,BIG_Y(A2)
		CMP.B		#1FH,BIG_Y(A2)
		NEXT_CHECK	BEQ,8,ELOGO_8

		PCHILD		WORK_N3,P_EASY,C_LOGO,160,16,2,0
		BSET.B		#ACTAT_ERACE,ACT_ATTR(A0)
		MOVE.B		#1FH,BIG_Y(A0)
		MOVE.B		#4,ACT_COUNT(A0)
		MOVE.W		X_POSITION(A2),X_POSITION(A0)
		MOVE.W		Y_POSITION(A2),Y_POSITION(A0)
		SUB.W		#4,X_POSITION(A2)
		ADD.W		#7,Y_POSITION(A2)
		MOVE.B		WORK_No(A0),WORK_N0(A2)

		SUB_WORK	WORK_N0,A2,0
		PCHILD		WORK_N3,P_EASY,C_LOGO,160,16,0,0
		MOVE.B		#0,ACT_COUNT(A0)
		MOVE.W		X_POSITION(A2),X_POSITION(A0)
		MOVE.W		Y_POSITION(A2),Y_POSITION(A0)
		SUB.W		#4,X_POSITION(A2)
		ADD.W		#15,Y_POSITION(A2)
		MOVE.B		WORK_No(A0),WORK_N0(A2)

		SUB_WORK	WORK_N1,A2,0
		PCHILD		WORK_N3,P_EASY,C_LOGO,160,16,0,0
		MOVE.B		#2,ACT_COUNT(A0)
		MOVE.W		X_POSITION(A2),X_POSITION(A0)
		MOVE.W		Y_POSITION(A2),Y_POSITION(A0)
		SUB.W		#4,X_POSITION(A2)
		ADD.W		#15,Y_POSITION(A2)
		MOVE.B		WORK_No(A0),WORK_N0(A2)

		RTS
ELOGO_8:
		SUB_WORK	WORK_N0,A0,0
		SUBQ.W		#1,Y_POSITION(A0)
		SUB_WORK	WORK_N1,A0,0
		SUBQ.W		#1,Y_POSITION(A0)
		SUB_WORK	WORK_N2,A2,0
		MOVEP.W		WORK_N0(A2),D0
		CLR.B		D0
		LEA.L		0(A5,D0.W),A0
		ADD.B		#20H,BIG_Y(A0)
		ADD.B		#20H,BIG_Y(A2)
		CMP.B		#0FFH,BIG_Y(A2)
		NEXT_CHECK	BEQ,9,ELOGO_9

		MOVEQ.L		#0,D0
		MOVE.B		COUNTRY_CODE(A5),D0
		ADDQ.W		#3,D0
		TST.B		MVS_FLAG(A5)
		BNE.S		ELOGO_8_1

		ADDQ.W		#3,D0
ELOGO_8_1:
		JSR.S		MESS_SET

		MOVE.W		#0EAH,D0
		ADD.B		COUNTRY_CODE(A5),D0
		JSR.S		MESS_SET

		SEC_TIMER_SET	6,WORD_TIMER
		RTS
ELOGO_9:
		SEC_TIMER	WORD_TIMER
		BCC.S		ELOGO_9_1

		JMP		DEMO_GAME_SET(PC)
ELOGO_9_1:
		RTS




ELOGO_V		EQU		8231H

ELOGO_POS1:
		ADD.W		#8200H,D4
		LEA.L		BACK1_BUFFER(A5),A1
		MOVE.W		WORD_TIMER(A6),D0
		ADD.W		D0,D0
		ADD.W		D0,D0
		ADD.W		D0,D0
		LEA.L		VP_DATA(PC),A2
		LEA.L		0(A2,D0.W),A2
		MOVE.L		(A2),D2
		MOVE.W		4(A2),D3
		EXT.L		D3
		MOVE.W		6(A2),D6
		MOVE.W		#160,D7
		SUB.W		D6,D7
		BRA		ELOGO_POS1_2
ELOGO_POS1_1:
		MOVE.W		D4,(A1)+
		CLR.W		(A1)+
		SUBQ.W		#1,D7
ELOGO_POS1_2:
		BNE.S		ELOGO_POS1_1

		MOVE.W		D6,D7
		BRA		ELOGO_POS1_4
ELOGO_POS1_3:
		MOVE.L		D2,D0
		LSR.L		#1,D0
		ADD.W		#8000H,D0
		AND.W		#0FF80H,D0
		ADD.W		#5,D0
		MOVE.W		D4,(A1)+
		MOVE.W		D0,(A1)+
		SUB.L		#100H,D2
		ADD.L		D3,D2
		SUBQ.W		#1,D7
ELOGO_POS1_4:
		BNE.S		ELOGO_POS1_3

		RTS

ELOGO_POS2:
		ADD.W		#8200H,D4
		LEA.L		BACK1_BUFFER(A5),A1
		MOVE.W		WORD_TIMER(A6),D0
		ADD.W		D0,D0
		ADD.W		D0,D0
		ADD.W		D0,D0
		LEA.L		VP_DATA(PC),A2
		LEA.L		0(A2,D0.W),A2
		MOVE.L		#176*100H,D2
		MOVE.W		4(A2),D3
		EXT.L		D3
;		MOVE.W		6(A2),D6
		MOVE.W		#160,D6
		MOVE.W		D6,D7
		BRA		ELOGO_POS2_2
ELOGO_POS2_1:
		MOVE.L		D2,D0
		LSR.L		#1,D0
		ADD.W		#8000H,D0
		AND.W		#0FF80H,D0
		ADD.W		#5,D0
		MOVE.W		D4,(A1)+
		MOVE.W		D0,(A1)+
		SUB.L		#100H,D2
		ADD.L		D3,D2
		SUBQ.W		#1,D7
ELOGO_POS2_2:
		BNE.S		ELOGO_POS2_1

		MOVE.W		#160,D7
		SUB.W		D6,D7
		BRA		ELOGO_POS2_4
ELOGO_POS2_3:
		MOVE.W		D4,(A1)+
		CLR.W		(A1)+
		SUBQ.W		#1,D7
ELOGO_POS2_4:
		BNE.S		ELOGO_POS2_3

		RTS







VP_DATA:

VP1		MACRO	SS,AD,LL
		DC.L	(56+LL*6)*100H
;		DC.L	SS
		DC.W	AD
		DC.W	LL*6+40
		ENDM

		VP1	-8000H,189,1
		VP1	-7000H,150,2
		VP1	-6000H,124,3
		VP1	-5000H,106,4
		VP1	-4000H,93,5
		VP1	-3000H,82,6
		VP1	-2000H,74,7
		VP1	-1000H,67,8
		VP1	0,61,9
		VP1	1000H,56,10

		VP1	2000H,52,11
		VP1	3000H,49,12
		VP1	4000H,46,13
		VP1	5000H,43,14
		VP1	6000H,40,15
		VP1	7000H,38,16
		VP1	8000H,36,17
		VP1	9000H,35,18
		VP1	0A000H,33,19
		VP1	0B000H,32,20




MESS_START:
		TST.W		INSERT_POS(A5)
		BMI.S		MESS_START_0		@@@@

		MOVEQ.L		#00111111B,D1
		AND.B		MAIN_COUNT+3(A5),D1
		BEQ.S		MESS_START_2

		CMP.B		#20,D1
		BEQ.S		MESS_START_1
MESS_START_0:
		RTS
MESS_START_1:
		MOVEQ.L		#1,D1
		TST.B		MVS_FLAG(A5)
		BEQ.S		MESS_START_2

		JSR.S		PLAYABLE

		MOVE.B		D0,D1
		ADDQ.W		#2,D1
MESS_START_2:
		MESS_ON
		MOVE.W		#0FH,D0
		ADD.W		INSERT_POS(A5),D0
		JSR.S		MESS_SET_INNER

		ADD.W		#9,D1
		MOVE.W		D1,D0
		JSR.S		MESS_SET_INNER

		CMP.B		#3,USER_REQUEST(A5)
		BNE.S		MESS_START_3

		MOVE.W		#11H,D0
		JSR.S		MESS_SET_INNER
MESS_START_3:
		MESS_OFF
		RTS



LM_EASY:
		JSR		MASTER_CHECK(PC)

		EASY_START


PM_EASY:
		JSR		MASTER_CHECK(PC)
		JSR.S		GET_REL_POS
		JSR.S		NEXT_ACTION
		JSR.S		SORT_SET
		RTS

MASTER_CHECK:
		MOVEQ.L		#01111111B,D0
		AND.B		PHASE+1(A5),D0
		CMP.B		MASTER_No(A6),D0
		BEQ.S		MASTER_OK

		CLR.W		TYPE(A6)
		ADDQ.W		#4,SP
MASTER_OK:
		RTS






		INCLUDE	WORK.INC



