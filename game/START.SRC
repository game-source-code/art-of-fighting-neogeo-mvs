;
;
;	    *** 044 STORY START DEMO ***
;
;


		XDEF	INIT_START_DEMO,START_DEMO

		XREF	?A5
;by MAIN
		XREF	LP_MAIN
;by GAME_DSP
		XREF	GAME_START_DISP
;by BACK
		XREF	BACK2_INIT,BACK2_SCROLL_SET
		XREF	BEFOR_SCROLL
;by HIT
		XREF	HERO1_HIT_JUDGE
		XREF	HERO2_HIT_JUDGE
;by MESSAGE
		XREF	MESS_SET,MESS_SET_INNER,FLAME_WRITE,FLAME_CLR
		XREF	FLAME_WRITED,FLAME_CLRD
;by LANG
		XREF	LANG_SET,LANG_SET_INNER
;by PHASE
		XREF	ACTIVE_TRIGER
;by MOVE
		XREF	ALL_COLOR_COM2,END_IN


		SECT	GAME,,C

		INCLUDE	SYS.INC
		INCLUDE	NEO_GEO.INC
		INCLUDE	LABEL.INC
		INCLUDE	HERO_TBL.INC
		INCLUDE	HK3_TBL.INC
		INCLUDE	MACRO.INC
		INCLUDE	EASY_MAC.INC
		INCLUDE	HFLAG.INC
		INCLUDE	HERO_STP.INC
		INCLUDE	CTRL_No.INC
		INCLUDE	SMACRO.INC
		INCLUDE	SCODE.INC




INIT_START_DEMO:
		MOVE.W	#31H,D0
		JSR.S	MESS_SET

		AND.B	#7FH,PHASE+1(A5)
		MOVE.B	#1,ST_DEMO_ON(A5)
		MOVE.B	#1,ST_DEMO_IN(A5)
		LEA.L	MLWORK1(A5),A6
		MOVE.W	#0,LW_D1(A6)
		CLR.L	LW_D4(A6)
		CLR.L	LW_D7(A6)
		CLR.B	ST_BACK_FLAG(A5)
		CLR.B	HERO1+HERO_STATE(A5)
		CLR.B	HERO2+HERO_STATE(A5)
		MOVE.W	STORY_STAGE(A5),D0
		ADD.W	D0,D0
		ADD.W	D0,D0
		MOVE.L	START_SEQ_TBL(PC,D0.W),ADRS1(A6)
		JSR	START_COMMAND(PC)

		MOVE.W	#1,LW_D0(A6)
		RTS

START_SEQ_TBL:
		DC.L	ST_SEQ1
		DC.L	ST_SEQ1
		DC.L	ST_SEQ2
		DC.L	ST_SEQ1
		DC.L	ST_SEQ3
		DC.L	ST_SEQ4
		DC.L	ST_SEQ1
		DC.L	ST_SEQ5
		DC.L	ST_SEQ6
		DC.L	ST_SEQ1
		DC.L	ST_SEQ7
		DC.L	ST_SEQ8
		DC.L	ST_SEQE

START_DEMO:

;		LW_D0	MAIN STEP	(time D2)
;		LW_D1	MESS STEP	(time D3)


		CMP.W	#12,STORY_STAGE(A5)
		BEQ.S	START_DEMO_101

		JSR.S	ACTIVE_TRIGER

		BTST.L	#B_A_BUTTON,D2
		BNE	ST_DEMO_CUT
START_DEMO_101:
		MOVE.W	LW_D4(A6),HERO1+PLAY_LEVER(A5)
		MOVE.W	LW_D5(A6),HERO2+PLAY_LEVER(A5)
		CLR.B	HERO1+PLAY_LEVER+2(A5)
		CLR.B	HERO2+PLAY_LEVER+2(A5)
		LEA.L	MLWORK1(A5),A6
		BSR	MESS_COMMAND
		BSR	SEQ_COMMAND

		TST.W	LW_D7(A6)
		BEQ.S	START_DEMO_102

		MOVE.B	#1,LP_MAIN_RUN(A5)
		RTS
START_DEMO_102:
		CMP.W	#12,STORY_STAGE(A5)
		BNE.S	START_DEMO_1

		LEA.L	HERO1(A5),A6
		JSR	PWORK_HIT

		LEA.L	HERO2(A5),A6
		JSR	PWORK_HIT
START_DEMO_1:
		LEA.L	HERO1(A5),A6
		JSR	LM_HERO

		LEA.L	HERO2(A5),A6
		JSR	LM_HERO

		TST.B	ST_BACK_FLAG(A5)
		BNE.S	STD_0000

		JSR	BEFOR_SCROLL(PC)
STD_0000:
		JSR	BACK2_SCROLL_SET(PC)
		JSR	LP_MAIN(PC)
		JSR	HERO1_HIT_JUDGE
		JSR	HERO2_HIT_JUDGE
		RTS




SEQ_COMMAND:
		MOVE.W	LW_D0(A6),D0
		ADD.W	D0,D0
		ADD.W	D0,D0
		JMP	SEQ_COM_VECTOR(PC,D0.W)
SEQ_COM_VECTOR:
		JMP	SEQ_WAIT(PC)
		JMP	SEQ_PAL_WAIT(PC)
		JMP	DESI_WAIT(PC)

SEQ_WAIT:
		SUBQ.W	#1,LW_D2(A6)
		BEQ	START_COMMAND

		RTS

SEQ_PAL_WAIT:
		PAL_WAIT	0FFH

		MOVE.W	#0,LW_D0(A6)
		JMP	START_COMMAND(PC)



MESS_COMMAND:
		CMP.W	#1,LW_D0(A6)
		BEQ.S	MESS_NON

		MOVE.W	LW_D1(A6),D0
		ADD.W	D0,D0
		ADD.W	D0,D0
		JMP	MESS_COM_VECTOR(PC,D0.W)
MESS_COM_VECTOR:
		JMP	MESS_NON(PC)
		JMP	FLAMEW_WAIT(PC)
		JMP	MESSW_WAIT(PC)
		JMP	FLAMEC_WAIT(PC)
MESS_NON:
		RTS
FLAMEW_WAIT:
		JSR	FLAME_WRITE(PC)
		BEQ.S	FLAMEW_WAIT_1

		RTS
FLAMEW_WAIT_1:
		MOVE.W	ST_DEMO_MESSP(A5),D0
		ADD.W	#0C7H,D0
		JSR.S	MESS_SET

		MOVE.W	#2,LW_D1(A6)	to wait
		MOVE.L	ADRS2(A6),A4
		MOVE.W	(A4)+,D0
		MOVE.W	(A4)+,LW_D3(A6)
		MOVE.L	A4,ADRS2(A6)
		JSR.S	LANG_SET

		RTS
MESSW_WAIT
		SUBQ.W	#1,LW_D3(A6)
		BEQ.S	MESSW_WAIT_1

		RTS
MESSW_WAIT_1:
		MOVE.L	ADRS2(A6),A4
		TST.W	(A4)
		BPL.S	MESSW_WAIT_2

		MOVE.W	#3,FLAME_COUNT(A5)
		MOVE.W	#1,FLAME_TYPE(A5)
		MOVE.W	#0,FLAME_END(A5)
		MOVE.W	#3,LW_D1(A6)
		RTS
MESSW_WAIT_2:
		MOVE.W	#2,FLAME_COUNT(A5)
		JSR.S	FLAME_WRITED
		JMP	FLAMEW_WAIT_1(PC)
FLAMEC_WAIT:
		JSR.S	FLAME_CLR
		BNE.S	FLAMEC_WAIT_1

		MOVE.W	#0,LW_D1(A6)
FLAMEC_WAIT_1:
		RTS


START_COMMAND:
		LEA.L	MLWORK1(A5),A6
		PEA.L	START_COMMAND(PC)
		MOVE.L	ADRS1(A6),A4
		MOVE.W	(A4)+,D0
		ADD.W	D0,D0
		ADD.W	D0,D0
		JMP	SC_VECTOR(PC,D0.W)
SC_VECTOR:
		JMP	SC_END(PC)	0:command end
		JMP	SC_OVER(PC)	1:game start
		JMP	SC_CTRL1(PC)	2:ctrl
		JMP	SC_CTRL2(PC)	3:ctrl
		JMP	ACT_ON1(PC)	4:
		JMP	ACT_ON2(PC)	5:
		JMP	MESS_COM1(PC)	6:
		JMP	MESS_COM2(PC)	7:
		JMP	LEV(PC)		8:
		JMP	DESI1(PC)	9:
		JMP	DESI2(PC)	10:
		JMP	BACK_OFF(PC)	11:
		JMP	BACK_ON(PC)	12:
		JMP	RIKI0(PC)	13:
		JMP	SC_EFFECT(PC)	14:
		JMP	SC_BGM(PC)	15:
		JMP	SC_PDEAD(PC)	16:
		JMP	SC_STOP(PC)	17:
		JMP	SC_END_END(PC)	18:
		JMP	SC_HAOH_ON(PC)	19:
		JMP	SC_COLOR0(PC)	20:
		JMP	SC_LIFE(PC)	21:
		JMP	SC_LIFE0(PC)	22:


SC_PDEAD:
		MOVE.L	A4,ADRS1(A6)
		JSR.S	PWORK_INIT
		RTS
SC_STOP:
		MOVE.L	A4,ADRS1(A6)
		MOVE.W	#1,LW_D7(A6)
		RTS
SC_END_END:
		ADDQ.L	#4,SP
		MOVE.W	#0,SUB_STEP(A5)
		MOVE.B	#01110000B,D0
		AND.B	PHASE+1(A5),D0
		OR.B	#00000111B,D0
		MOVE.B	D0,PHASE+1(A5)
		STEP_SAVE	0,END_IN
		RTS

SC_HAOH_ON:
		MOVE.L	A4,ADRS1(A6)
		MOVE.B	#96,HERO1+HERO_SPIRIT(A5)
		MOVE.B	#96,HERO2+HERO_SPIRIT(A5)
		MOVE.B	#1,HAOH_USE(A5)
		RTS
SC_LIFE:
		MOVE.L	A4,ADRS1(A6)
		MOVE.W	#31,HERO1+HERO_LIFE(A5)
		MOVE.W	#31,HERO2+HERO_LIFE(A5)
		RTS
SC_LIFE0:
		MOVE.L	A4,ADRS1(A6)
		BSR	ENE_GET

		MOVE.W	#0,HERO_LIFE(A6)
		RTS

PWAIT:
		MOVE.L	A4,ADRS1(A6)
		MOVE.W	#1,LW_D0(A6)
		ADDQ.L	#4,SP
		RTS

SC_COLOR:
		MOVE.L	A4,ADRS1(A6)
		MOVE.W	#FADE_OUT+CC_SPEED3,D0
		JSR	ALL_COLOR_COM2(PC)
		RTS

SC_COLOR0:
		MOVE.L	A4,ADRS1(A6)
		MOVE.W	#FADE_IN+CC_SPEED3,D0
		JSR	ALL_COLOR_COM2(PC)
		RTS


SC_EFFECT:
		MOVE.W	(A4)+,D0
		MOVE.L	A4,ADRS1(A6)
		JSR.S	SET_SOUND
		RTS
SC_BGM:
		MOVE.W	(A4)+,D0
		MOVE.L	A4,ADRS1(A6)
		JSR.S	SET_BGM
		RTS
RIKI0:
		MOVE.L	A4,ADRS1(A6)
		MOVE.W	#0,HERO1+HERO_SPIRIT(A5)
		MOVE.W	#0,HERO2+HERO_SPIRIT(A5)
		RTS

SC_END:
		MOVE.W	(A4)+,LW_D2(A6)
		MOVE.L	A4,ADRS1(A6)
		MOVE.W	#0,LW_D0(A6)
		ADDQ.L	#4,SP
		RTS

ST_DEMO_CUT:
		JSR.S	PWORK_INIT

		MOVE.B	#1,LP_MAIN_RUN(A5)
		BSR	SC_OVER_SUB

		MOVE.W	STORY_STAGE(A5),D0
		ADD.W	D0,D0
		MOVE.W	CUT_FADE-2(PC,D0.W),D0
		BEQ.S	STD_CUT_1

		JSR.S	SET_BGM
STD_CUT_1:
		MOVE.W	#1,SUB_STEP(A5)
		OR.B	#80H,PHASE+1(A5)
		RTS
CUT_FADE:
		DC.W	0		1
		DC.W	0		2
		DC.W	0		2
		DC.W	0		3
		DC.W	144H*2		4
		DC.W	144H*2		4
		DC.W	182H*2		5
		DC.W	0		6
		DC.W	0		6
		DC.W	0		7
		DC.W	0		8
		DC.W	0		8

SC_OVER:
		ADDQ.L	#4,SP
		MOVE.W	#9,SUB_STEP(A5)
		MOVE.B	#120,BYTE_TIMER(A6)
		JSR	GAME_START_DISP(PC)
SC_OVER_SUB:
		MOVE.W	HERO1+RIKI_MAX(A5),HERO1+HERO_SPIRIT(A5)
		MOVE.W	HERO2+RIKI_MAX(A5),HERO2+HERO_SPIRIT(A5)
		CLR.B	ST_DEMO_IN(A5)
		CLR.B	ST_BACK_FLAG(A5)
		RTS
SC_CTRL1:
		MOVE.W	(A4)+,D4
		MOVE.L	A4,ADRS1(A6)
		BSR	PLAY_GET

		JSR	CTRL_CHANGE

		RTS
SC_CTRL2:
		MOVE.W	(A4)+,D4
		MOVE.L	A4,ADRS1(A6)
		BSR	ENE_GET

		JSR	CTRL_CHANGE

		RTS
ACT_ON1:
		MOVE.L	A4,ADRS1(A6)
		BSR	PLAY_GET

		MOVE.B	#1,ACT_DEC(A4)
		RTS
ACT_ON2:
		MOVE.L	A4,ADRS1(A6)
		BSR	ENE_GET

		MOVE.B	#1,ACT_DEC(A4)
		RTS
LEV:
		MOVE.L	(A4)+,D0
		MOVE.L	A4,ADRS1(A6)
		TST.W	STORY_PLAYER(A5)
		BEQ.S	LEV_1

		SWAP	D0
		MOVE.L	D0,D1
		MOVE.L	D0,D2
		AND.L	#0F3F3F3F3H,D0
		AND.L	#008080808H,D1
		AND.L	#004040404H,D2
		LSR.L	#1,D1
		LSL.L	#1,D2
		OR.L	D1,D0
		OR.L	D2,D0
LEV_1:
		MOVE.L	D0,LW_D4(A6)
		RTS

PLAY_GET:
		LEA.L	HERO1(A5),A6
		TST.W	STORY_PLAYER(A5)
		BEQ.S	PLAY_GET_1

		LEA.L	HERO2(A5),A6
PLAY_GET_1:
		MOVEP.W	WORK_N0(A6),D0
		CLR.B	D0
		LEA.L	0(A5,D0.W),A4
		RTS
ENE_GET:
		LEA.L	HERO1(A5),A6
		TST.W	STORY_PLAYER(A5)
		BNE.S	ENE_GET_1

		LEA.L	HERO2(A5),A6
ENE_GET_1:
		MOVEP.W	WORK_N0(A6),D0
		CLR.B	D0
		LEA.L	0(A5,D0.W),A4
		RTS

MESS_COM1:
		MOVE.L	(A4)+,ADRS2(A6)
		MOVE.W	HERO1+HERO_TYPE(A5),D1
		MOVE.W	STORY_PLAYER(A5),D0
		BEQ.S	MESS_COM1_1

		MOVE.W	HERO2+HERO_TYPE(A5),D1
MESS_COM1_1:
		TST.W	D1
		BEQ.S	MESS_COM1_2

		MOVE.L	(A4),ADRS2(A6)
MESS_COM1_2:
		ADDQ.L	#4,A4
		MOVE.L	A4,ADRS1(A6)
		BRA.S	MESS_COM_ON
MESS_COM2:
		MOVE.L	(A4)+,ADRS2(A6)
		MOVE.L	A4,ADRS1(A6)
		MOVE.W	STORY_PLAYER(A5),D0
		EOR.W	#1,D0
MESS_COM_ON:
		MOVE.W	#7000H+20H*3+4,D1
		MOVE.W	D0,ST_DEMO_MESSP(A5)
		BEQ.S	MESS_COM_ON_1

		MOVE.W	#7000H+20H*19+4,D1
MESS_COM_ON_1:
		MOVE.W	#0,FLAME_COUNT(A5)
		MOVE.W	#1,FLAME_TYPE(A5)
		MOVE.W	D1,FLAME_POS(A5)
		MOVE.W	#3,FLAME_END(A5)
		MOVE.W	#1,LW_D1(A6)
		RTS


DESI2:
		MOVE.L	A4,ADRS1(A6)
		PCHILD	WORK_N1,P_DESI,175H,174H,50H,0BCH,0	E1_DESI1
		MOVE.W	#7H,VX(A0)
		MOVE.L	#5C000H,VZ(A0)
		MOVE.W	#40H,Z_POSITION(A0)
		BSR	DESI_SET

		MOVE.W	#2,LW_D0(A6)
		ADDQ.L	#4,SP
		RTS
DESI1:
		MOVE.L	A4,ADRS1(A6)
		PCHILD	WORK_N0,P_DESI,174H,174H,50H,0BCH,0	E1_DESI1
		MOVE.W	#5H,VX(A0)
		MOVE.L	#7C000H,VZ(A0)
		MOVE.W	#80H,Z_POSITION(A0)
DESI_SET:
		BSET.B	#ACTAT_FLIP,ACT_ATTR(A0)
		TST.W	STORY_PLAYER(A5)
		BEQ.S	DESI1_1

		MOVE.W	#17CH,X_POSITION(A0)
		NEG.W	VX(A0)
		BCLR.B	#ACTAT_FLIP,ACT_ATTR(A0)
DESI1_1:
		RTS

DESI_WAIT:
		SUB_WORK	WORK_N1,A0,0
		TST.W	TYPE(A0)
		BNE	DESI_WAIT_1

		CLR.B	ST_BACK_FLAG(A5)
		JSR	START_COMMAND(PC)
DESI_WAIT_1:
		RTS

BACK_OFF:
		MOVE.L	A4,ADRS1(A6)
		MOVE.B	#1,ST_BACK_FLAG(A5)
		RTS
BACK_ON:
		MOVE.L	A4,ADRS1(A6)
		MOVE.B	#0,ST_BACK_FLAG(A5)
		RTS


ST_SEQ1:
		DC.W	15,17FH*2	BGM
		DC.W	2,1DH
		DC.W	3,1
		DC.W	0,0
		DC.W	0,30

		DC.W	7
		DC.L	ST1E1
		DC.W	4
		DC.W	0,90

		DC.W	6
		DC.L	ST1H1,ST1H2
		DC.W	0,140

		DC.W	7
		DC.L	ST1E1_2
		DC.W	0,90

		DC.W	8,505H,0
		DC.W	3,40H
		DC.W	0,10

		DC.W	8,0,0
		DC.W	0,40
;		DC.W	15,144H*2
		DC.W	1

ST1E1:		DC.W	110*3,60,-1
ST1E1_2:	DC.W	114*3,60,-1
ST1H1:		DC.W	111*3,110,-1
ST1H2:		DC.W	112*3,50,113*3,60,-1
ST1H1_1		DC.W	115*3,60,-1
ST1H2_1		DC.W	116*3,60,-1

ST_SEQ2:
		DC.W	15,0EH*2
		DC.W	2,1DH
		DC.W	3,1DH
		DC.W	0,0

		DC.W	6
		DC.L	ST2H1,ST2H2
		DC.W	0,90

		DC.W	7
		DC.L	ST2E2
		DC.W	0,70

		DC.W	4
		DC.W	5
		DC.W	0,90

		DC.W	7
		DC.L	ST2E2_1
		DC.W	0,130

		DC.W	8,404H,0
		DC.W	0,2

		DC.W	8,0,0
		DC.W	0,2

		DC.W	8,404H,0
		DC.W	3,20H
		DC.W	0,2

		DC.W	8,0,0
		DC.W	0,30
		DC.W	1

ST2H1:		DC.W	117*3,60,-1
ST2H2:		DC.W	118*3,60,-1
ST2E2:		DC.W	119*3,40,-1
ST2E2_1:	DC.W	120*3,50,121*3,50,-1

ST_SEQ3:
		DC.W	15,0FH*2
		DC.W	2,1
		DC.W	3,1
		DC.W	0,10

		DC.W	8,505H,505H
		DC.W	0,10

		DC.W	8,0,0
		DC.W	0,70

		DC.W	8,202H,0
		DC.W	0,1

		DC.W	3,21H
		DC.W	0,31

		DC.W	8,0,0
		DC.W	0,7

		DC.W	8,404H,0
		DC.W	0,2

		DC.W	8,0,0
		DC.W	0,2

		DC.W	8,404H,0
		DC.W	3,34H
		DC.W	0,2

		DC.W	8,0,0
		DC.W	0,28

		DC.W	6
		DC.L	ST3H1,ST3H2
		DC.W	0,90

		DC.W	7
		DC.L	ST3E3
		DC.W	14,40H*2
		DC.W	0,90

		DC.W	6
		DC.L	ST3H1_1,ST3H2_1
		DC.W	0,90
		DC.W	1

ST3H1:		DC.W	123*3,60,-1
ST3H2:		DC.W	125*3,60,-1
ST3H1_1:	DC.W	124*3,60,-1
ST3H2_1:	DC.W	126*3,60,-1
ST3E3:		DC.W	122*3,60,-1


ST_SEQ4:
		DC.W	15,127H*2
		DC.W	2,1DH
		DC.W	3,1DH
		DC.W	0,0
		DC.W	0,30

		DC.W	7
		DC.L	ST4E4
		DC.W	4
		DC.W	0,90

		DC.W	6
		DC.L	ST4H1,ST4H2
		DC.W	0,150

		DC.W	7
		DC.L	ST4E4_1
		DC.W	0,20

		DC.W	5
		DC.W	0,40

		DC.W	8,505H,0
		DC.W	3,31H
		DC.W	0,2

		DC.W	8,0,0
		DC.W	0,30

;		DC.W	15,144H*2
		DC.W	1

ST4E4:		DC.W	127*3,60,-1
ST4E4_1:	DC.W	132*3,60,-1
ST4H1:		DC.W	128*3,60,129*3,60,-1
ST4H2:		DC.W	130*3,60,131*3,60,-1

ST_SEQ5:
		DC.W	15,180H*2
		DC.W	2,1DH
		DC.W	3,1DH
		DC.W	0,0
		DC.W	0,30

		DC.W	6
		DC.L	ST5H1,ST5H2
		DC.W	0,60

		DC.W	4
		DC.W	0,80

		DC.W	7
		DC.L	ST5E5
		DC.W	0,100

		DC.W	8,404H,0
		DC.W	0,2

		DC.W	8,0,0
		DC.W	0,2

		DC.W	8,404H,0
		DC.W	3,34H
		DC.W	0,2

		DC.W	8,0,0
		DC.W	0,30

;		DC.W	15,182H*2
		DC.W	1

ST5H1:		DC.W	133*3,50,134*3,60,-1
ST5H2:		DC.W	135*3,50,136*3,60,-1
ST5E5:		DC.W	137*3,60,-1

ST_SEQ6:
		DC.W	15,12H*2
		DC.W	2,1DH
		DC.W	3,1
		DC.W	0,0
		DC.W	0,30

		DC.W	7
		DC.L	ST6E6
		DC.W	4
		DC.W	3,1DH
		DC.W	0,130

		DC.W	8,505H,909H
		DC.W	0,10

		DC.W	8,0,0
		DC.W	0,60
		DC.W	1

ST6E6:		DC.W	138*3,60,-1


ST_SEQ7:
		DC.W	15,13H*2
		DC.W	2,1DH
		DC.W	3,1
		DC.W	0,0
		DC.W	0,30

		DC.W	7
		DC.L	ST7E7
		DC.W	0,12

		DC.W	4
		DC.W	3,1AH
		DC.W	0,130

		DC.W	8,505H,0
		DC.W	3,21H
		DC.W	0,2

		DC.W	8,0,0
		DC.W	0,30
		DC.W	1

ST7E7:		DC.W	139*3,60,-1

ST_SEQ8:
		DC.W	15,14H*2
		DC.W	2,1
		DC.W	3,1
		DC.W	0,0
		DC.W	0,30

		DC.W	6
		DC.L	ST8H1,ST8H2
		DC.W	0,62

		DC.W	13
		DC.W	2,19H
		DC.W	3,19H
		DC.W	8,1000H,1000H
		DC.W	0,240

		DC.W	8,0,0
		DC.W	0,10

		DC.W	8,0,909H
		DC.W	2,31H
		DC.W	0,10

		DC.W	8,0,0
		DC.W	0,40
		DC.W	1

ST8H1:		DC.W	140*3,50,141*3,60,-1
ST8H2:		DC.W	142*3,50,143*3,60,-1

ST_SEQE:
		DC.W	20
		DC.W	21
		DC.W	2,1
		DC.W	3,1
		DC.W	0,0
		DC.W	0,30

		DC.W	2,34H
		DC.W	0,34

		DC.W	21
		DC.W	2,23H
		DC.W	0,35

		DC.W	8,808H,0
		DC.W	0,45

		DC.W	21
		DC.W	2,24H
		DC.W	0,13

		DC.W	3,13H
		DC.W	0,23

		DC.W	8,404H,0
		DC.W	0,60

		DC.W	19
		DC.W	2,43H
		DC.W	0,30

		DC.W	17
		DC.W	14,5*2		E_DI
		DC.W	14,2*2
		DC.W	14,8*2
		DC.W	0,60

		DC.W	16
		DC.W	18




		INCLUDE	WORK.INC



