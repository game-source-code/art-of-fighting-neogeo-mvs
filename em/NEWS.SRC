;
;
;	    *** HIT JUDGE NEWS ***
;
;

		XDEF	INIT_NEWS,NEWS

		XREF	?A5

		SECT	HK3,,C


		INCLUDE	LABEL.INC


INIT_NEWS:
		CLR.W	TARGET_X(A6)
		CLR.W	TARGET_Y(A6)
		RTS

NEWS:
		MOVE.L	ANOTHER_PLAYER+HERO_OFFSET(A6),A1
		LEA.L	HERO_OFFSET(A6),A2
;		TST.B	SPECIAL_USE(A6)
;		BNE	TAMA_NEWS

		MOVE.W	ACT_CTRL(A1),D0
		CMP.W	#20H,D0
		BCC.S	NEWS_2
NON_ATTACK:
		CLR.W	TARGET_X(A6)
		CLR.W	TARGET_Y(A6)
		RTS
NEWS_2:
		TST.B	TARGET_X(A6)
		BEQ	NEWS_21

		TST.B	TARGET_X+1(A6)
		BNE	NNN_1

		MOVEP.W	WORK_N0(A1),D1
		CLR.B	D1
		BTST.B	#4,ACT_FLAG(A5,D1.W)
		BEQ	NNN_END

		MOVE.B	#1,TARGET_X+1(A6)
		RTS
NNN_1:
		MOVEP.W	WORK_N0(A1),D1
		CLR.B	D1
		BTST.B	#4,ACT_FLAG(A5,D1.W)
		BNE.S	NNN_END

		MOVE.B	#2,TARGET_X+1(A6)
		CLR.B	TARGET_Y(A6)
NNN_END:
		RTS
NEWS_21:
		MOVE.B	D0,TARGET_X(A6)
		LEA.L	ATTACK_ATTR1(PC),A0
		TST.W	HERO_TYPE(A1)
		BEQ.S	NEWS_3

		LEA.L	ATTACK_ATTR2(PC),A0
NEWS_3:
		ADD.W	D0,D0
		ADD.W	D0,D0
		LEA.L	-80H(A0,D0.W),A0
		MOVE.B	(A0),D0
		BEQ	NON_ATTACK
		BMI	MOVE_ATTACK

NML_ATTACK:
		MOVEP.W	WORK_N2(A3),D3
		CLR.B	D3
		MOVE.W	2(A0),D4
		BSR	NML_SUB
		BCC.S	NON_ATTACK

		MOVE.B	1(A0),TARGET_Y+1(A6)
		MOVE.B	#1,TARGET_Y(A6)
		RTS
NML_SUB:
		MOVEP.W	WORK_N0(A1),D0
		CLR.B	D0
		BTST.B	#ACTAT_FLIP,ACT_ATTR(A5,D0.W)
		BEQ.S	NML_ATTACK_1

		NEG.W	D4
		MOVE.W	HIT_RIGHT(A5,D3.W),D2
		ADD.W	X_POSITION(A1),D4
		CMP.W	D2,D4
		RTS
NML_ATTACK_1:
		MOVE.W	HIT_LEFT(A5,D3.W),D2
		ADD.W	X_POSITION(A1),D4
		CMP.W	D4,D2
		RTS

MOVE_ATTACK:
		CMP.B	#0FFH,D0
		BEQ	HISSATU
;
		MOVE.W	2(A0),D1
		MOVEP.W	WORK_N0(A1),D0
		CLR.B	D0
		BTST.B	#ACTAT_FLIP,ACT_ATTR(A5,D0.W)
		BEQ.S	MOVE_AT_1

		NEG.W	D1
MOVE_AT_1:
		ADD.W	X_POSITION(A1),D1
		SUB.W	X_POSITION(A2),D1
		BMI	MI_LEFT

		MOVE.W	VX(A5,D0.W),D2
		BMI	NON_ATTACK
		BEQ	NML_ATTACK
MOVE_AT_2:
		EXT.L	D1
		DIVS	D2,D1
		MOVE.B	D1,TARGET_Y+1(A6)
		MOVE.B	#1,TARGET_Y(A6)
		RTS
MI_LEFT:
		MOVE.W	VX(A5,D0.W),D2
		BGT	NON_ATTACK
		BEQ	NML_ATTACK
		BRA.S	MOVE_AT_2

HISSATU:
		MOVE.W	#100H,TARGET_Y(A6)
		RTS

ATTACK		MACRO	TYPE,TIME,OFS
		DC.B	TYPE,TIME
		DC.W	OFS
		ENDM

ATTACK_ATTR1:
;P1
		ATTACK	01,01,15+61		P1
		ATTACK	01,04,29+75		P2
		ATTACK	01,01,15+61		P1
		ATTACK	01,02,27+58		P3
		ATTACK	01,11,32+55		P4
		ATTACK	01,11,32+55		P4
		ATTACK	01,05,26+31		P5
		ATTACK	01,03,31+70		DP
		ATTACK	80H,02,26+37		JP
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0

		ATTACK	01,06,80		K1
		ATTACK	01,23,94		K2
		ATTACK	01,06,80		K1
		ATTACK	01,43,56		K3
		ATTACK	01,04,106		K4
		ATTACK	01,04,106		K4
		ATTACK	0,0,0
		ATTACK	01,04,104		DK
		ATTACK	80H,03,71		JK
		ATTACK	80H,03,89		JB
		ATTACK	81H,18,-104		JT
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0

		ATTACK	0FFH,0,0			KOO
		ATTACK	0FFH,38,102		HIEN
		ATTACK	01,01,110		ZAIRETU
		ATTACK	0FFH,0,0			HAOO


ATTACK_ATTR2:
		ATTACK	01,01,19+63		P1
		ATTACK	01,01,115		P2
		ATTACK	01,01,19+63		P1
		ATTACK	01,08,77		P3
		ATTACK	01,05,73		P4
		ATTACK	01,05,73		P4
		ATTACK	01,05,32+34		P5
		ATTACK	01,03,101		DP
		ATTACK	80H,03,89		JP
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0

		ATTACK	01,05,81		K1
		ATTACK	01,05,99		K2
		ATTACK	01,05,81		K1
		ATTACK	01,11,127	K3
		ATTACK	01,03,105		K4
		ATTACK	01,03,105		K4
		ATTACK	0,0,0
		ATTACK	01,04,103		DK
		ATTACK	80H,03,86		JK
		ATTACK	80H,11,90		JB
		ATTACK	82H,18,-86		JT
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0
		ATTACK	0,0,0

		ATTACK	0FFH,0,0			KOO
		ATTACK	0FFH,38,102		HIEN
		ATTACK	01,01,110		ZAIRETU
		ATTACK	0FFH,0,0			HAOO


		INCLUDE	WORK.INC


